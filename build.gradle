import static org.jetbrains.intellij.tasks.RunPluginVerifierTask.FailureLevel.*

buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        maven { url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
        maven { url "https://maven.aliyun.com/nexus/content/groups/public/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "de.undercouch:gradle-download-task:4.1.1"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.7.2"
    id "com.adarshr.test-logger" version "3.0.0"
    id "de.undercouch.download" version "4.1.1"
    id "org.jetbrains.kotlin.jvm" version "1.4.31"
    id "idea"
}

testlogger {
    theme 'mocha'
}

repositories {
    mavenLocal()
    maven { url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    maven { url "https://maven.aliyun.com/nexus/content/groups/public/" }
    maven { url "https://repo.eclipse.org/content/groups/releases/" }
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://www.jetbrains.com/intellij-repository/snapshots" }
}

def pluginsDependencies = ['java',
                           'java-i18n',
                           'JavaScriptLanguage',
                           'DatabaseTools',
                           'CSS',
                           'platform-images',
                           'Groovy',
                           'properties',
                           'yaml',
                           "org.jetbrains.kotlin:203-$kotlinVersion-release-IJ5981.133-1",
                           'org.intellij.scala:2020.3.21',
                           'Dart:203.5981.155',
                           'org.jetbrains.plugins.ruby:203.5981.155',
                           'com.jetbrains.php:203.5981.155',
                           'com.jetbrains.sh:203.5981.37',
                           'com.jetbrains.plugins.jade:203.5981.155',
                           'org.jetbrains.plugins.go-template:203.5981.155',
]

intellij {
    pluginName name

    version ideaVersion
    //localPath = '/Users/izhangzhihao/Library/Application Support/JetBrains/Toolbox/apps/IDEA-U/ch-0/201.6668.121/IntelliJ IDEA 2020.1 EAP.app/Contents'
    //localPath = '/Users/izhangzhihao/Library/Application Support/JetBrains/Toolbox/apps/CLion/ch-0/201.6668.126/CLion.app/Contents'
    updateSinceUntilBuild = false

    setPlugins(*pluginsDependencies)
}

runIde {
    systemProperty "idea.auto.reload.plugins", false
}

publishPlugin {
    def authenticationToken = "$System.env.token"
    token authenticationToken
    channels publishChannels
}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}

dependencies {
    implementation("org.eclipse.mylyn.github:org.eclipse.egit.github.core:5.10.0.202012080955-r") {
        exclude module: "gson"
    }
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation("io.kotlintest:kotlintest:2.0.7")
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

[compileKotlin, compileTestKotlin]*.kotlinOptions {
    languageVersion = kotlinLanguageVersion
    apiVersion = kotlinTargetVersion
    jvmTarget = javaVersion
    freeCompilerArgs = ["-Xskip-runtime-version-check", "-Xjsr305=strict"]
}

runPluginVerifier {
    ideVersions = "$pluginVerifierIdeVersions"
    failureLevel = COMPATIBILITY_PROBLEMS
}
