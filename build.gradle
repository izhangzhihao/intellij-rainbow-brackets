buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        maven { url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
        maven { url "https://maven.aliyun.com/nexus/content/groups/public/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "de.undercouch:gradle-download-task:4.0.2"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.4.21"
    id "com.adarshr.test-logger" version "2.0.0"
    id "de.undercouch.download" version "4.0.2"
    id "org.jetbrains.kotlin.jvm" version "1.3.71"
    id "jacoco"
    id "idea"
}

apply from: "$rootDir/verifier.gradle"

testlogger {
    theme 'mocha'
}

repositories {
    mavenLocal()
    maven { url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    maven { url "https://maven.aliyun.com/nexus/content/groups/public/" }
    maven { url "https://repo.eclipse.org/content/groups/releases/" }
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://www.jetbrains.com/intellij-repository/snapshots" }
}

def pluginsDependencies = ['java',
                           'java-i18n',
                           'JavaScriptLanguage',
                           'DatabaseTools',
                           'CSS',
                           'platform-images',
                           'Groovy',
                           'properties',
                           'yaml',
                           "org.jetbrains.kotlin:$kotlinVersion-release-IJ2020.1-1",
                           'org.intellij.scala:2020.2.5',
                           'Dart:202.4357.23',
                           'org.jetbrains.plugins.ruby:202.4357.23',
                           'com.jetbrains.php:202.4357.23'
]

intellij {
    pluginName name

    version ideaVersion
    //localPath = '/Users/izhangzhihao/Library/Application Support/JetBrains/Toolbox/apps/IDEA-U/ch-0/201.6668.121/IntelliJ IDEA 2020.1 EAP.app/Contents'
    //localPath = '/Users/izhangzhihao/Library/Application Support/JetBrains/Toolbox/apps/CLion/ch-0/201.6668.126/CLion.app/Contents'
    updateSinceUntilBuild = false

    setPlugins(*pluginsDependencies)
}

publishPlugin {
    def authenticationToken = "$System.env.token"
    token authenticationToken
    channels publishChannels
}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}


def gistFile = { url, name ->
    File file = new File("libs/${name}.jar")
    file.parentFile.mkdirs()
    if (!file.exists()) {
        new URL(url).withInputStream { downloadStream ->
            file.withOutputStream { fileOut ->
                fileOut << downloadStream
            }
        }
    }
    files(file.absolutePath)
}

dependencies {
    implementation("org.eclipse.mylyn.github:org.eclipse.egit.github.core:5.5.0.201909110433-r") {
        exclude module: "gson"
    }
    compileOnly gistFile('https://gist.github.com/izhangzhihao/e9e775b9fccc621de58557a726d84339/raw/1e700b2a23eb4fa8d859f1062956dc08e860cb0b/clion.jar', 'clion')
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation("io.kotlintest:kotlintest:2.0.7")
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

[compileKotlin, compileTestKotlin]*.kotlinOptions {
    languageVersion = kotlinLanguageVersion
    apiVersion = kotlinTargetVersion
    jvmTarget = javaVersion
    freeCompilerArgs = ["-Xskip-runtime-version-check"]
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            '**/settings/**',
                            '**/util/**',
                    ])
                })
        )
    }
}


pluginVerifier {
    pluginFileName = "$rootProject.name-$version"
    ides = []
    verifierVersion = "1.241"
}

check.dependsOn pluginVerifier